PGM (&MODE &FROM &TO &KEY &DELETE)

  DCL &MODE    TYPE(*CHAR) LEN(4)
  DCL &FROM    TYPE(*CHAR) LEN(128)
  DCL &TO      TYPE(*CHAR) LEN(128)
  DCL &KEY     TYPE(*CHAR) LEN(32)
  DCL &DELETE  TYPE(*CHAR) LEN(4)
  DCL &CMD     TYPE(*CHAR) LEN(512)
  DCL &SUCCESS TYPE(*CHAR) LEN(1) VALUE('1')

  DCLPRCOPT  USRPRF(*USER) AUT(*EXCLUDE) DFTACTGRP(*NO) ACTGRP(*NEW)

  RMVLNK OBJLNK(&TO)
  MONMSG MSGID(CPF0000)

  IF (&KEY = ' ' *AND &MODE *NE '*B64') THEN(DO)
    SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('No key available') TOPGMQ(*PRV)
    CHGVAR &SUCCESS '0'
  ENDDO

  If (&SUCCESS = '1' ) THEN(DO)
    ADDENVVAR  ENVVAR(QIBM_QSH_CMD_OUTPUT) VALUE('NONE') REPLACE(*YES)
    ADDENVVAR  ENVVAR(QIBM_QSH_CMD_ESCAPE_MSG) VALUE(Y) REPLACE(*YES)


    IF (&MODE = '*ENC') THEN(DO)
      CHGVAR &CMD VALUE('openssl des3 -salt -in ' +
                         *CAT &FROM *TCAT ' -out ' *CAT &TO *TCAT +
                         ' -k ' *CAT &KEY)
      STRQSH CMD(&CMD)
      MONMSG MSGID(CPF0000) EXEC(DO)
        SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Error while encryption') TOPGMQ(*PRV)
        CHGVAR &SUCCESS '0'
      ENDDO
      IF (&DELETE = '*YES' *AND &SUCCESS = '1' ) THEN(DO)
        RMVLNK OBJLNK(&FROM)
        MONMSG MSGID(CPF0000)
      ENDDO
    ENDDO

    IF (&MODE *EQ '*DEC') THEN(DO)
      CHGVAR &CMD VALUE('openssl des3 -d -salt -in ' +
                        *CAT &FROM *TCAT ' -out ' *CAT &TO *TCAT +
                        ' -k ' *CAT &KEY)
      STRQSH CMD(&CMD)
      MONMSG MSGID(CPF0000) EXEC(DO)
        SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Error while decryption') TOPGMQ(*PRV)
        CHGVAR &SUCCESS '0'
      ENDDO
      IF (&DELETE = '*YES' *AND &SUCCESS = '1' ) THEN(DO)
        RMVLNK OBJLNK(&FROM)
        MONMSG MSGID(CPF0000)
      ENDDO
    ENDDO

    IF (&MODE *EQ '*B64') THEN(DO)
      CHGVAR &CMD  VALUE('openssl base64 < ' *CAT +
                          &FROM *TCAT ' > ' *CAT &TO)
      STRQSH     CMD(&CMD)
      MONMSG     MSGID(CPF0000) EXEC(DO)
        SNDPGMMSG  MSGID(CPF9898) MSGF(QCPFMSG) MSGDTA('Error occured while encoding') TOPGMQ(*PRV)
        CHGVAR &SUCCESS '0'
      ENDDO
      IF (&DELETE = '*YES' *AND &SUCCESS = '1') THEN(DO)
        RMVLNK OBJLNK(&FROM)
        MONMSG MSGID(CPF0000)
      ENDDO
    ENDDO
  ENDDO

ENDPGM